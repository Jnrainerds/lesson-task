.版本 2
.支持库 xplib
.支持库 eAPI
.支持库 iext2
.支持库 spec
.支持库 iconv
.支持库 internet

.程序集 窗口程序集1
.程序集变量 初始化完毕, 逻辑型
.程序集变量 SUI_Window, SUI_Window
.程序集变量 SUI_MsgBox, SUI_MsgBox

.子程序 __启动窗口_创建完毕
.局部变量 字体列表, 文本型, , "0"
.局部变量 i, 整数型
.局部变量 存在微软雅黑, 逻辑型

XP风格 (1)  ' 对于自带控件使用xp风格
初始化完毕 ＝ 假
SUI_Initialize (#默认底图, , 假)  ' 初始化SUI界面引擎

字体列表 ＝ 取系统字体列表 ()  ' 防止不存在微软雅黑字体
存在微软雅黑 ＝ 假
.计次循环首 (取数组成员数 (字体列表), i)
    .如果真 (字体列表 [i] ＝ “微软雅黑”)
        存在微软雅黑 ＝ 真
        跳出循环 ()
    .如果真结束

.计次循环尾 ()
.如果真 (取反 (存在微软雅黑))
    SUIFontName (“宋体”)
.如果真结束

SUITemplete (#SUI_Templete_Metro)  ' 对于SUI控件使用metro风格
SUI_LoadResource ()  ' 加载资源
pIcon ＝ SUILoadImage (#图标)
pEditIco ＝ SUILoadImage (#用户名)
pEditIco2 ＝ SUILoadImage (#文件夹)
pEditIco3 ＝ SUILoadImage (#密码)
pBtnIco ＝ SUILoadImage (#设置)
SUILoadImages (#皮肤按钮, { 24, 24, 24 }, 真, , , pSkin)

SUI_Window.Ptr (, 到整数 (&MsgCallBack))  ' 绑定事件
SUI_Window.Create (_启动窗口.取窗口句柄 (), 假, 假, 假, 假, , , 假, “信息输出”, SUITextColor (), 真, 真, 假, 0)  ' 创建信息输出窗口
SUI_Window.Icon (pIcon)
.如果 (取反 (文件是否存在 (取运行目录 () ＋ “/配置.ini”)))
    SUI_Window.Show (真)
    线程关闭API (线程创建API (0, 0, &初始化, 0, 0, 0))  ' 第一次启动初始化
.否则
    直接启动 ()  ' 不是第一次启动 直接进入启动器主界面
.如果结束



.子程序 MsgCallBack, 逻辑型
.参数 hwnd, 整数型
.参数 wMsg, 整数型
.参数 wParam, 整数型
.参数 lParam, 整数型
.局部变量 i, 整数型

.判断开始 (wMsg ＝ #WM_DESTROY)
    ' 销毁自己的资源
    SUIDeleteImage (pIcon)
    .计次循环首 (取数组成员数 (pSkin), i)
        SUIDeleteImage (pSkin [i])
    .计次循环尾 ()
    SUIDeleteImage (pEditIco)
    SUIDeleteImage (pBtnIco)
    SUIDeleteImage (pEditIco2)
    SUIDeleteImage (pEditIco3)
    ' 销毁引擎
    SUI_Remove (真)
    输出调试文本 (“End”)
.默认

.判断结束
返回 (假)

.子程序 输出
.参数 文本颜色, 整数型
.参数 文本, 文本型
.局部变量 字符格式, 字符格式

字符格式.文本颜色 ＝ 文本颜色
超级编辑框1.起始选择位置 ＝ 取文本长度 (超级编辑框1.内容)
超级编辑框1.置选择区字符格式 (字符格式)
超级编辑框1.被选择文本 ＝ 文本
.如果真 (超级编辑框1.取行数 () ≥ 2000)
    超级编辑框1.内容 ＝ “”
.如果真结束
超级编辑框1.发送信息 (277, 7, 0)

.子程序 初始化
.局部变量 信息, 文本型, , "3"
.局部变量 MsgRet, 整数型
.局部变量 是否64位, 逻辑型
.局部变量 最佳内存, 文本型

输出 (#白色, “初始化中...” ＋ #换行符)
延迟 (500)
输出 (#白色, “正在检查系统信息...” ＋ #换行符)
是否64位 ＝ 是否64位 ()
.如果 (是否64位)
    输出 (#黄色, “您正在使用64位系统” ＋ #换行符)
.否则
    输出 (#黄色, “您正在使用32位系统” ＋ #换行符)
.如果结束
延迟 (500)
输出 (#白色, “正在检查java信息...” ＋ #换行符)
java信息 (信息)
延迟 (500)
.如果 (信息 [1] ＝ “”)
    MsgRet ＝ SUI_MsgBox.Create (_启动窗口.取窗口句柄 (), “mclauncher+”, “您也许还未安装java运行环境” ＋ #换行符 ＋ “选择” ＋ #引号 ＋ “是” ＋ #引号 ＋ “将会跳转到java官方网站” ＋ #换行符 ＋ “如果您知道您已经安装 请选择” ＋ #引号 ＋ “否” ＋ #引号 ＋ “将使用默认值”, 真, 真, #询问图标, 假, { “否”, “是” }, 1, 假, , 假)
    .如果真 (SUI_MsgBox.Ret (MsgRet) ＝ 2)
        打开指定网址 (“http://www.java.com”)
    .如果真结束
    写配置项 (取运行目录 () ＋ “\” ＋ “配置.ini”, “mcluncher+”, “java路径”, “”)
    写配置项 (取运行目录 () ＋ “\” ＋ “配置.ini”, “mcluncher+”, “启动模式”, “不强制”)
.否则
    .如果 (信息 [3] ＝ “64位”)
        输出 (#黄色, “您已安装64位java ” ＋ 信息 [1] ＋ #换行符)
        写配置项 (取运行目录 () ＋ “\” ＋ “配置.ini”, “mcluncher+”, “启动模式”, “强制64位”)
    .否则
        输出 (#黄色, “您已安装32位java ” ＋ 信息 [1] ＋ #换行符)
        .如果真 (是否64位)
            输出 (#黄色, “您正在使用64位系统 推荐到www.java.com下载64位java以增强流畅度” ＋ #换行符)
        .如果真结束
        写配置项 (取运行目录 () ＋ “\” ＋ “配置.ini”, “mcluncher+”, “启动模式”, “不强制”)
    .如果结束
    .如果真 (信息 [1] ≠ “1.7”)
        输出 (#黄色, “您也许没有使用java7 推荐到www.java.com下载此版本以便支持更多mod” ＋ #换行符)
    .如果真结束
    输出 (#黄色, “路径:” ＋ 信息 [2] ＋ #换行符)
    写配置项 (取运行目录 () ＋ “\” ＋ “配置.ini”, “mcluncher+”, “java路径”, 信息 [2])
.如果结束
延迟 (500)
输出 (#黄色, “检查是否安装minecraft...” ＋ #换行符)
.如果 (是否安装游戏 ())
    输出 (#黄色, “您已安装minecraft!” ＋ #换行符)
.否则
    SUI_MsgBox.Create (_启动窗口.取窗口句柄 (), “mclauncher+”, “您还未安装minecraft” ＋ #换行符 ＋ “请初始化完成后进入” ＋ #引号 ＋ “下载中心” ＋ #引号 ＋ “来安装minecraft~” ＋ #换行符 ＋ “如果您确信您已经安装了minecraft” ＋ #换行符 ＋ “ 请将本文件和.minecraft放在同一文件夹内”, 真, 真, #信息图标, 假, { “确定” }, 1, 假, , 假)
.如果结束
延迟 (500)
输出 (#白色, “正在为您设置最佳内存大小...” ＋ #换行符)
最佳内存 ＝ 最佳内存 (是否64位)
输出 (#黄色, “您的最佳内存设定为” ＋ 最佳内存 ＋ “M” ＋ #换行符)
写配置项 (取运行目录 () ＋ “\” ＋ “配置.ini”, “mcluncher+”, “内存”, 最佳内存)
延迟 (500)
输出 (#黄色, “初始化完毕!单击左键继续...” ＋ #换行符)
初始化完毕 ＝ 真

.子程序 _超级编辑框1_鼠标左键被按下, 逻辑型
.参数 横向位置, 整数型
.参数 纵向位置, 整数型
.参数 功能键状态, 整数型

.如果真 (初始化完毕)
    初始化完毕 ＝ 假
    直接启动 ()
.如果真结束


.子程序 直接启动

SUI_Window.Show (假)  ' 隐藏信息输出窗体
载入 (窗口1, , 假)
SUIDispatchMessage ()  ' 消息循环


.子程序 是否安装游戏, 逻辑型

返回 (文件是否存在 (取运行目录 () ＋ “/.minecraft”))

.子程序 下载中心初始化
.局部变量 i, 整数型
.局部变量 分割, 文本型, , "0"
.局部变量 分割2, 文本型, , "0"
.局部变量 文本, 文本型

SUI_Window.Show (真)  ' 显示信息输出窗体
输出 (#白色, “正在读取下载中心的列表...” ＋ #换行符)


文本 ＝ 到文本 (编码转换 (HTTP读文件 (“http://mclauncherplus.sinaapp.com/api/?m=download”), #编码_UTF_8, #编码_GB2312, ))
分割 ＝ 分割文本 (文本, “|”, )
下载个数 ＝ 取数组成员数 (分割)
重定义数组 (下载项目, 假, 下载个数)
.计次循环首 (下载个数, i)
    分割2 ＝ 分割文本 (分割 [i], “<>”, )
    下载项目 [i].标题 ＝ 分割2 [1]
    下载项目 [i].地址 ＝ 分割2 [2]
.计次循环尾 ()
SUI_Window.Show (假)
.如果 (下载个数 ＝ 0)
    SUI_MsgBox.Create (_启动窗口.取窗口句柄 (), “mclauncher+”, “获取失败,也许是网络问题”, 真, 真, #信息图标, 假, { “确定” }, 1, 假, , 假)
.否则
    载入 (窗口3, 窗口1, 真)  ' 载入下载中心
.如果结束

